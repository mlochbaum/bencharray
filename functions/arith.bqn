# Arithmetic functions

basename ← "arith"

ns_v   ← •Import "../params/ns_v.bqn"
types  ← •Import "../params/cputype.bqn"
GetFns ← ⋈ •Import "../params/arith.bqn"˙
GetInp ← {name⇐"Length" ⋄ range⇐𝕩}

# Measurement functions
MSmall ← { 𝕊 type‿fn:
  ⟨float,size,max,GetGen⇐GetGenSmall⟩ ← type ⋄ size⇐
  TimeParams ⇐ {
    # Use one generator for both arguments
    float ∨ ×˙⊸≠fn ? l ← 1‿2/⟨fn, GetGen𝕩⟩ ;
    # In integer × arguments need to interact to suppress overflow: use
    # two parallel generators but run each twice so each argument sees
    # both sets of random numbers
    ⟨fn⟩ ∾ ⊣‿{•internal.Squeeze(××(⌊max÷|𝕨)⌊|)𝕩} {𝕎○𝕏˜}⟜GetGen¨ <𝕩
  }
}
_mGen ← { GenPair _𝕣 type‿fn:
  ⟨size,GetGen⟩ ← type ⋄ size⇐
  TimeParams ⇐ { ⟨fn⟩∾getGen GenPair 𝕩 }
}
MLarge ← (⋈˜{𝕎𝕩})_mGen
MLScal ← {⟨{⊑𝕏}𝕎1,𝕎𝕩⟩}_mGen

Bench ← {
  ext‿fns‿range‿measure‿plotpre‿plotname ← 𝕩
  name ⇐ basename∾ext
  params ⇐ types ⋈ fnobjs ← GetFns fns
  input ⇐ GetInp range
  result ⇐ ns_v
  Measure‿PlotPre⇐
  plotOpts ⇐ {
    title ⇐ plotname
    x ⇐ types
    y ⇐ result
    labels ⇐ {𝕩.Disp¨ 𝕩.num} fnobjs
  }
}
Summary ← Bench∘{
  ext⇐𝕨 ⋄ fns‿measure‿plotname ⇐ 𝕩
  Range ⇐ {<(2⋆3+15)÷𝕩.size}
  PlotPre ⇐ {> types.num⊸⋈¨ <˘⍉1⊑⎉1𝕩}
}

⟨
  "-summary" Summary ⟨×‿+‿⌊‿<,      MSmall, "Arithmetic with no overflow, 32KB"⟩
  "-small"   Summary ⟨×‿+‿-‿⌊‿⌈,    MSmall, "Arithmetic with no overflow, 32KB"⟩
  "-over"    Summary ⟨×‿+‿-,        MLarge, "Arithmetic with overflow, 32KB"⟩
  "-scal"    Summary ⟨×‿+‿⌊‿<,      MLScal, "Scalar-list arithmetic allowing overflow, 32KB"⟩
  "-compare" Summary ⟨<‿>‿=‿≠‿≤‿≥,  MLarge, "Comparisons, 32KB"⟩
  "-slow"    Summary ⟨(⋆⁼)‿⋆‿√‿|‿÷, MLarge, "Slow arithmetic, 32KB"⟩
⟩
