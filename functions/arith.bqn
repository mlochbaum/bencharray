# Dyadic arithmetic functions

basename ← "arith"

ns_v   ← •Import "../params/ns_v.bqn"
types‿itypes ← •Import⟜"../params/cputype.bqn"¨ ⟨⟩‿⟨"Input type"⟩
GetFns ← ⋈ •Import "../params/arith.bqn"˙
GetInp ← {name⇐"Length" ⋄ range⇐𝕩}

# Measurement functions
MSmall ← { 𝕊 type‿fn:
  ⟨float,size,max,GetGen⇐GetGenSmall⟩ ← type ⋄ size⇐
  TimeParams ⇐ {
    # Use one generator for both arguments
    float ∨ ×˙⊸≠fn ? l ← 1‿2/⟨fn, GetGen𝕩⟩ ;
    # In integer × arguments need to interact to suppress overflow: use
    # two parallel generators but run each twice so each argument sees
    # both sets of random numbers
    ⟨fn⟩ ∾ ⊣‿{•internal.Squeeze(××(⌊max÷|𝕨)⌊|)𝕩} {𝕎○𝕏˜}⟜GetGen¨ <𝕩
  }
}
_mGen ← { GenPair _𝕣 type‿fn:
  ⟨size,GetGen⟩ ← type ⋄ size⇐
  TimeParams ⇐ { ⟨fn⟩∾getGen GenPair 𝕩 }
}
MLarge ← (⋈˜{𝕎𝕩})_mGen
MLScal ← {⟨{⊑𝕏}𝕎1,𝕎𝕩⟩}_mGen

Bench ← {
  ext‿fns‿range‿measure‿plotpre‿plotname‿itype ← 𝕩
  name ⇐ basename∾ext
  types ← •Import "../params/cputype.bqn"
  params ⇐ ⟨types, GetFns fns⟩
  input ⇐ GetInp range
  result ⇐ ns_v
  Measure‿PlotPre⇐
  plotOpts ⇐ {
    title ⇐ plotname
    x ⇐ itype ⊑ types‿itypes
    y ⇐ result
    key ⇐ 1⊑params
  }
}
Summary ← Bench∘{
  ext⇐𝕨 ⋄ fns‿measure‿itype‿plotname ⇐ 𝕩
  Range ⇐ {<(2⋆3+15)÷𝕩.size}
  PlotPre ⇐ {> types.num⊸⋈¨ <˘⍉1⊑⎉1𝕩}
}

⟨
  "-summary" Summary ⟨×‿+‿⌊‿<,      MSmall, 0, "Arithmetic with no overflow, 32KB"⟩
  "-small"   Summary ⟨×‿+‿-‿⌊‿⌈,    MSmall, 0, "Arithmetic with no overflow, 32KB"⟩
  "-over"    Summary ⟨×‿+‿-,        MLarge, 1, "Arithmetic with overflow, 32KB"⟩
  "-scal"    Summary ⟨×‿+‿⌊‿<,      MLScal, 1, "Scalar-list arithmetic allowing overflow, 32KB"⟩
  "-compare" Summary ⟨<‿>‿=‿≠‿≤‿≥,  MLarge, 0, "Comparisons, 32KB"⟩
  "-slow"    Summary ⟨(⋆⁼)‿⋆‿√‿|‿÷, MLarge, 1, "Slow arithmetic, 32KB"⟩
⟩
